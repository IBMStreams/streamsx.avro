namespace com.ibm.streamsx.avro.sample;

use com.ibm.streamsx.avro::*;


/**
 * Convert tuple to Avro. Avro output is printed to the console output.
 */
public composite TupleToAvroSample
{
	type 
		/**
		 * Sample tuple type
		 */
		tweetT = rstring username, rstring tweet, int64 tweettime;

	graph
		/* Generate 100 tweets with a changing value for every tweet */
		stream<tweetT> GenerateTweet = Beacon()
		{
			param
				iterations: 100u;
			output
				GenerateTweet : username = "Frank", tweet = "Hi, this is a cool tweet! "
					+(rstring) IterationCount(), tweettime = (int64)(1048298232ul + IterationCount());
		}

		// Batch the incoming tuples into Avro tuples with embedded schema
		stream<blob avroMessage> ConvertToAvro = TupleToAvro(GenerateTweet) {
			param
				avroMessageSchemaFile: getThisToolkitDir()+"/etc/twitter.avsc";
				embedAvroSchema: true;
				bytesPerMessage: 5000l;
		}

		// Display the retrieved AVRO message
		() as DisplayAvro = Custom(ConvertToAvro as I)
		{
			logic
				onTuple I: println((rstring) I);
		}

}
